
&НаСервере
Процедура СброситьВсеЗарегистрированныеНаСервере()
	
	Узлы = Новый Массив;
	        
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
				 |	ОбменМеждуСистемамиБрониирования.Ссылка КАК Ссылка
				 |ИЗ
				 |	ПланОбмена.ОбменМеждуСистемамиБрониирования КАК ОбменМеждуСистемамиБрониирования
				 |ГДЕ
				 |	НЕ ОбменМеждуСистемамиБрониирования.ЭтотУзел";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
	
		Узлы.Добавить(Выборка.Ссылка);
	
	КонецЦикла;                       
	
	Объекты = Новый Массив;
	        
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	Бронирование.Ссылка КАК Ссылка
	               |ИЗ
	               |	Документ.Бронирование КАК Бронирование";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
	
		Объекты.Добавить(Выборка.Ссылка);
	
	КонецЦикла;                       
	
	ПланыОбмена.УдалитьРегистрациюИзменений(Узлы, Объекты);	
	
КонецПроцедуры

&НаКлиенте
Процедура СброситьВсеЗарегистрированные(Команда)
	СброситьВсеЗарегистрированныеНаСервере();
КонецПроцедуры

&НаСервере
Функция ВыгрузитьДанныеНаСервере()
	
	Поток = Новый ПотокВПамяти;
		
	Запись = Новый ЗаписьXML;
	Запись.ОткрытьПоток(Поток);
	
	ДополнительныеОбъекты = Новый Массив;
	
	ЗаписьСообщения = ПланыОбмена.СоздатьЗаписьСообщения();
	ЗаписьСообщения.НачатьЗапись(Запись, Получатель);
	
	Выборка = ПланыОбмена.ВыбратьИзменения(Получатель, ЗаписьСообщения.НомерСообщения);
	
	Пока Выборка.Следующий() Цикл
	
		Данные = Выборка.Получить();   
		
		ДобавитьДополнительныйОбъект(ДополнительныеОбъекты, Данные.ТипНомера);
		
		Для каждого Строка Из Данные.Постояльцы Цикл
		
			ДобавитьДополнительныйОбъект(ДополнительныеОбъекты, Строка.Постоялец);
		
		КонецЦикла;
		
		ЗаписатьXML(Запись, Данные);
	
	КонецЦикла;                     
	
	Для каждого Ссылка Из ДополнительныеОбъекты Цикл
	
		Данные = Ссылка.ПолучитьОбъект();
		
		ЗаписатьXML(Запись, Данные);
	
	КонецЦикла;
	
	ЗаписьСообщения.ЗакончитьЗапись();  
	
	Запись.Закрыть();
	
	Возврат ПоместитьВоВременноеХранилище(Поток.ЗакрытьИПолучитьДвоичныеДанные());

КонецФункции

Процедура ДобавитьДополнительныйОбъект(ДополнительныеОбъекты, Ссылка)

	Если ДополнительныеОбъекты.Найти(Ссылка) <> Неопределено Тогда
		
	    Возврат;		
	
	КонецЕсли;	
	
	ДополнительныеОбъекты.Добавить(Ссылка);
	
КонецПроцедуры


&НаКлиенте
Процедура ВыгрузитьДанные(Команда)    
	
	Если Получатель.Пустая() Тогда
	
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Получатель должен быть заполнен";
		Сообщение.Поле = "Получатель";
		Сообщение.Сообщить();
	
	КонецЕсли;
	
	АдресДанных = ВыгрузитьДанныеНаСервере();     
	
	ПараметрыДиалога = Новый ПараметрыДиалогаПолученияФайлов;
	
	ПолучитьФайлССервераАсинх(АдресДанных, "Выгрузка.xml", ПараметрыДиалога);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьДанныеНаСервере(Адрес)

	ДвоичныеДанные = ПолучитьИзВременногоХранилища(Адрес);
	
	Чтение = Новый ЧтениеXML;
	Чтение.ОткрытьПоток(ДвоичныеДанные.ОткрытьПотокДляЧтения());
	
	ЧтениеСообщения = ПланыОбмена.СоздатьЧтениеСообщения();
	ЧтениеСообщения.НачатьЧтение(Чтение, ДопустимыйНомерСообщения.Очередной);
	
	Пока ВозможностьЧтенияXML(Чтение) Цикл
	
		Данные = ПрочитатьXML(Чтение); 
		
		Если ТипЗнч(Данные) = Тип("ДокументОбъект.Бронирование") Тогда
		
			Данные.ОбменДанными.Отправитель = ЧтениеСообщения.Отправитель;

		КонецЕсли; 
		
		Данные.ОбменДанными.Загрузка = Истина;
		Данные.Записать();
	
	КонецЦикла;           
	
	ЧтениеСообщения.ЗакончитьЧтение();
	
	Чтение.Закрыть();

КонецПроцедуры

&НаКлиенте
Асинх Процедура ЗагрузитьДанные(Команда)
	
	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов;
	ПараметрыДиалога.Фильтр = "ФайлВыгруки (*.xml)|*.xml";	
	
	Результат = Ждать ПоместитьФайлНаСерверАсинх(,,,ПараметрыДиалога); 
	
	Если Результат = Неопределено Тогда
	
		Возврат;	
	
	КонецЕсли;
	
	Если Результат.ПомещениеФайлаОтменено Тогда
	
		Возврат;	
	
	КонецЕсли;
	
	ЗагрузитьДанныеНаСервере(Результат.Адрес);
	
	ПоказатьОповещениеПользователя("ДанныеЗагружены");
	
КонецПроцедуры
